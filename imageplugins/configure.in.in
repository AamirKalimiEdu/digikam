#MIN_CONFIG(3.1)
CXXFLAGS="$CXXFLAGS $KDE_DEFAULT_CXXFLAGS"

#------------------------------------------------------------------
# Digikam headers detection
#------------------------------------------------------------------

# first test if we are in the toplevel directory has digikam

AC_MSG_CHECKING([for digiKam in our sources])

if test -d $srcdir/digikam; then
   AC_MSG_RESULT([found])
   have_digikam_toplevel=yes
   have_digikam=yes
   LIBDIGIKAM_CFLAGS='-I$(top_srcdir)/digikam/utilities/imageeditor -I$(top_srcdir)/digikam/libs/guibuilder -I$(top_srcdir)/digikam/libs/curves -I$(top_srcdir)/digikam/libs/histogram -I$(top_srcdir)/digikam/libs/levels -I$(top_srcdir)/digikam/libs/filters -I$(top_srcdir)/digikam/libs/widgets'
   LIBDIGIKAM_LIBS='$(top_builddir)/digikam/digikam/libdigikam.la'
   LIBDIGIKAM_LIBS_DEP='$(LIBDIGIKAM_LIBS)'
   AC_SUBST(LIBDIGIKAM_CFLAGS)
   AC_SUBST(LIBDIGIKAM_LIBS)
   AC_SUBST(LIBDIGIKAM_LIBS_DEP)
else
   AC_MSG_RESULT([not found])
   have_digikam_toplevel=no
   AC_MSG_CHECKING(if digiKam is installed)
   KDE_CHECK_HEADER(digikam/imageplugin.h,  have_digikam=yes,  have_digikam=no)
   if test "x$have_digikam" = "xno"; then
      AC_MSG_WARN([Can't find digiKam headers. 
      Please install digiKam from kdeextragear-3 to be able to compile DigikamImagePlugins]);
      DO_NOT_COMPILE="$DO_NOT_COMPILE digikamimageplugins"
   else
      LIBDIGIKAM_CFLAGS=''
      LIBDIGIKAM_LIBS='-ldigikam'
      LIBDIGIKAM_LIBS_DEP='$(LIBDIGIKAM_LIBS)'
      AC_SUBST(LIBDIGIKAM_CFLAGS)
      AC_SUBST(LIBDIGIKAM_LIBS)
      AC_SUBST(LIBDIGIKAM_LIBS_DEP)
   fi
fi   

if test "x$have_digikam_toplevel" == "xyes"; then
   AC_DEFINE(HAVE_DIGIKAM_TOPLEVEL, 1, [Using digiKam sources from toplevel])
fi

#---------------------------------------------------------
# imlib2-detection
#---------------------------------------------------------
# checking for Imlib2...

AC_DEFUN([KDE_FIND_IMLIB2_CONFIG],
   [AC_MSG_CHECKING([for imlib2-config])

imlib2_config_given=NO
ac_imlib2_config=""

AC_ARG_WITH(imlib2-config,
        [  --with-imlib2-config     directory where \"imlib2-config\" lies ],
        [  ac_imlib2_config="$withval/imlib2-config"
           imlib2_config_given=yes
        ])

if test "$imlib2_config_given" = "yes"; then
  if test ! -r $ac_imlib2_config; then
    AC_MSG_RESULT(wrong file specified)
    ac_imlib2_config=""
  else
    IMLIB2_CFLAGS=`$ac_imlib2_config --cflags`
    LIB_IMLIB2=`$ac_imlib2_config --libs`
    AC_SUBST(LIB_IMLIB2)
    AC_SUBST(IMLIB2_CFLAGS)
    AC_MSG_RESULT($ac_imlib2_config)
  fi
else
  ac_imlib2_config=`which imlib2-config 2>/dev/null`
  if test -z "$ac_imlib2_config"; then
    AC_MSG_RESULT(no)
  else
    dummy=`echo "$ac_imlib2_config" | grep '/imlib2-config'`
    if test -z "$dummy"; then
      AC_MSG_RESULT(no)
      ac_imlib2_config=""
    else
      LIB_IMLIB2=`$ac_imlib2_config --libs`
      IMLIB2_CFLAGS=`$ac_imlib2_config --cflags`
      imlib2_config=`echo "$LIB_IMLIB2" | grep lImlib2`
      if test -z "$imlib2_config"; then
        AC_MSG_RESULT(no)
        ac_imlib2_config=""
      else
        AC_SUBST(LIB_IMLIB2)
        AC_SUBST(IMLIB2_CFLAGS)
        AC_MSG_RESULT($ac_imlib2_config)
      fi
    fi
  fi
fi

])

dnl Check if Imlib2 is installed properly
KDE_FIND_IMLIB2_CONFIG


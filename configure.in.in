#MIN_CONFIG(3)

AM_INIT_AUTOMAKE(digikam,0.6.3)

dnl ------------------------------------------------------------
dnl libraries needed
dnl ------------------------------------------------------------


#------------------------------------------------------------------
# gphoto2 detection
#------------------------------------------------------------------

dnl Check for gPhoto2 configuration

AC_PATH_PROG(GPHOTO_CONFIG,gphoto2-config)
AC_PATH_PROG(GPHOTO_PORT_CONFIG,gphoto2-port-config)
if test -n "${GPHOTO_CONFIG}"; then
  GPHOTO_CFLAGS="`$GPHOTO_CONFIG --cflags`"
  AC_SUBST(GPHOTO_CFLAGS)
  LIB_GPHOTO="`$GPHOTO_CONFIG --libs` `$GPHOTO_PORT_CONFIG --libs`"
  AC_SUBST(LIB_GPHOTO)
else
  AC_MSG_WARN([gPhoto2 not found.])
  DO_NOT_COMPILE="digikam $DO_NOT_COMPILE"
fi


CXXFLAGS="$CXXFLAGS -DQT_CLEAN_NAMESPACE"


#------------------------------------------------------------------
# KIPI support (KDE Image Plugin Interface) instead DigikamPlugins
#------------------------------------------------------------------

AC_MSG_CHECKING(if Digikam should be compiled with KIPI support)
AC_ARG_ENABLE(kipi,
[  --enable-kipi           Use KIPI instead DigikamPlugins, 'KDE Image Plugin Interface' (default=yes)],
[if test "$enableval" = yes; then
  AC_MSG_RESULT(yes)
  want_kipi="yes"
else
  AC_MSG_RESULT(no)
fi], [AC_MSG_RESULT(yes); want_kipi="yes"])

if test "$want_kipi" = "yes"; then
  KDE_CHECK_HEADER(libkipi/interface.h,
    have_kipi=yes,
    have_kipi=no)

  if test "$have_kipi" = "yes"; then
	AC_DEFINE(HAVE_KIPI, 1, [If we have libkipi installed])
	LIB_KIPI="-lkipi"
	AC_SUBST(LIB_KIPI)
  else
    AC_WARN([Can't find KIPI headers, Digikam won't be compiled with KIPI support])
  fi
fi

#------------------------------------------------------------------
# Check for Libkexif
#------------------------------------------------------------------

KDE_CHECK_HEADER(libkexif/kexif.h, have_kexif=yes, have_kexif=no)

if test "$have_kexif" = "no"; then
  AC_WARN([Can't find Libkexif headers, Digikam won't be compiled.])
  DO_NOT_COMPILE="digikam $DO_NOT_COMPILE"
else
  LIB_KEXIF="-lkexif"
  AC_SUBST(LIB_KEXIF)
fi

